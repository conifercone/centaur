//file:noinspection GroovyAssignabilityCheck
//file:noinspection GrUnresolvedAccess


import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import java.nio.charset.StandardCharsets
import java.time.LocalDateTime

plugins {
    alias(libs.plugins.dependencyManagement) apply(false)
    alias(libs.plugins.springboot) apply(false)
    alias(libs.plugins.lombok) apply(false)
    alias(libs.plugins.protobuf) apply(false)
    alias(libs.plugins.license) apply(false)
    alias(libs.plugins.kotlinJvm) apply(false)
    alias(libs.plugins.kotlinPluginSpring) apply(false)
    alias(libs.plugins.kotlinPluginJpa) apply(false)
}

allprojects { project ->
    group "${GROUP}"
    version "${VERSION}"

    // 定义maven仓库地址
    repositories {
        mavenCentral()
    }

    configurations.configureEach {
        resolutionStrategy {
            // 配置缓存实时更新
            cacheChangingModulesFor 0, "seconds"
            cacheDynamicVersionsFor 0, "seconds"
        }
        resolutionStrategy.dependencySubstitution {
            substitute module("org.springframework.boot:spring-boot-starter-tomcat:" + libs.versions.springbootVersion.get()) using module("org.springframework.boot:spring-boot-starter-undertow:" + libs.versions.springbootVersion.get())
        }
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

subprojects { project ->
    apply plugin: libs.plugins.getJava().get().getPluginId()
    apply plugin: libs.plugins.getJavaLibrary().get().getPluginId()
    apply plugin: libs.plugins.getIdea().get().getPluginId()
    apply plugin: libs.plugins.getDependencyManagement().get().getPluginId()
    apply plugin: libs.plugins.getLombok().get().getPluginId()
    apply plugin: libs.plugins.getLicense().get().getPluginId()
    apply plugin: libs.plugins.getKotlinJvm().get().getPluginId()
    apply plugin: libs.plugins.getKotlinPluginSpring().get().getPluginId()
    apply plugin: libs.plugins.getKotlinPluginJpa().get().getPluginId()

    // java工具链配置
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(Javadoc).configureEach {
        def javaDocDir = layout.buildDirectory.dir('docs/javadoc').get()
        delete(javaDocDir)
        options.encoding = StandardCharsets.UTF_8.name()
        options.charSet = StandardCharsets.UTF_8.name()
        failOnError = false
        destinationDir = file(javaDocDir)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = StandardCharsets.UTF_8.name()
    }

    compileJava.dependsOn(processResources)

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions.freeCompilerArgs.add("-Xjsr305=strict")
    }

    // 打包自定义设置
    jar {
        into("META-INF/") {
            from rootProject.file("LICENSE")
        }
        manifest {
            attributes 'Implementation-Title': archiveBaseName
            attributes 'Implementation-Version': archiveVersion
            attributes 'Built-Gradle': gradle.gradleVersion
            attributes 'Build-OS': System.getProperty("os.name")
            attributes 'Build-Jdk': System.getProperty("java.version")
            attributes 'Build-Timestamp': LocalDateTime.now().format("yyyy-MM-dd HH:mm:ss")
            attributes 'Automatic-Module-Name': "${project.group}.${project.name.replaceAll("-", ".")}"
        }
    }

    tasks.configureEach {
        if ("bootJar".is(it.name)) {
            manifest {
                attributes 'Implementation-Title': archiveBaseName
                attributes 'Implementation-Version': archiveVersion
                attributes 'Application-Version': archiveVersion
                attributes 'Built-Gradle': gradle.gradleVersion
                attributes 'Build-OS': System.getProperty("os.name")
                attributes 'Build-Jdk': System.getProperty("java.version")
                attributes 'Build-Timestamp': LocalDateTime.now().format("yyyy-MM-dd HH:mm:ss")
                attributes 'Automatic-Module-Name': "${project.group}.${project.name.replaceAll("-", ".")}"
            }
        }
    }

    tasks.configureEach {
        if ("bootBuildImage".is(it.name)) {
            imageName.set("conifercone/${project.name}:${project.version}")
        }
    }

    // 源代码许可配置
    license {
        encoding = StandardCharsets.UTF_8.name()
        ignoreFailures = true
        header = rootProject.file("SOURCE_CODE_HEAD.txt")
        includes(["**/*.java", "**/*.kt", "**/*.xml", "**/*.yml"])
        excludes(["**/client/api/grpc/*.java", "**/*_.java", "**/*run.xml"])
        mapping "java", "SLASHSTAR_STYLE"
        mapping "kt", "SLASHSTAR_STYLE"
        mapping "xml", "XML_STYLE"
        mapping "yml", "SCRIPT_STYLE"
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.organization = "kaiyu.shan@outlook.com"
    }

    dependencyManagement {
        imports {
            mavenBom(libs.springBootDependencies.get().toString())
            mavenBom(libs.springCloudDependencies.get().toString())
            mavenBom(libs.bullBom.get().toString())
            mavenBom(libs.grpcBom.get().toString())
            mavenBom(libs.protobufBom.get().toString())
            mavenBom(libs.guavaBom.get().toString())
        }
    }

    // 通用依赖
    dependencies {
        implementation libs.springboot
        implementation libs.springBootLog4j2
        implementation libs.disruptor
        implementation libs.bundles.jackson
        implementation libs.jetbrainsAnnotations
        implementation libs.apiguardian
        implementation libs.guava
        implementation libs.commonsLang3
        implementation libs.commonsIo
        implementation libs.bullMapTransformer
        implementation libs.bullBeanTransformer
        implementation libs.jacksonModuleKotlin
        implementation libs.kotlinReflect
        testImplementation libs.junitJupiter
        annotationProcessor libs.springBootConfigurationProcessor
    }

    test {
        useJUnitPlatform()
    }
}
